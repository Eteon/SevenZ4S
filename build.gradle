plugins {
    id 'scala'
    id 'idea'
    id 'maven-publish'
    id 'maven'
    id 'signing'
}

group 'fun.mactavish'
version '0.5.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.scala-lang', name: 'scala-library', version: '2.13.4'
    implementation group: 'net.sf.sevenzipjbinding', name: 'sevenzipjbinding', version: '16.02-2.01'
    implementation group: 'net.sf.sevenzipjbinding', name: 'sevenzipjbinding-all-platforms', version: '16.02-2.01'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.0'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'sevenz4s'
            from components.java

            pom {
                name = 'SevenZ4S'
                description = 'A 7Z compression library for Scala.'
                url = 'https://github.com/gonearewe/SevenZ4S'
                licenses {
                    license {
                        name = 'GNU LESSER GENERAL PUBLIC LICENSE, Version 2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.html'
                    }
                }
                developers {
                    developer {
                        id = 'gonearewe'
                        name = 'John Mactavish'
                        email = 'cristmactavish@outlook.com'
                    }
                }
                scm {
                    connection = 'https://github.com/gonearewe/SevenZ4S.git'
                    url = 'https://github.com/gonearewe/SevenZ4S'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username "abcde"
                password "xxxxx"
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}

